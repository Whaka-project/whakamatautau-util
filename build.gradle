apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.whaka'
archivesBaseName = "whakamatautau-util"
version = '0.0.9-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.3'
	compile 'com.google.guava:guava:18.0'
	compile 'org.slf4j:slf4j-api:1.7.12'
	compile 'org.hamcrest:hamcrest-library:1.3'
	compile 'org.mockito:mockito-all:1.10.19'
	compile 'junit:junit:4.11'
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
	testCompile 'cglib:cglib:3.1'
	testCompile 'org.objenesis:objenesis:1.3'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.6'
}

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

def osslogin = hasProperty('ossrhUsername') ? ossrhUsername : ""
def osspswrd = hasProperty('ossrhPassword') ? ossrhPassword : ""

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment)
			}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: osslogin, password: osspswrd)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: osslogin, password: osspswrd)
			}

			pom.project {
				name archivesBaseName
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'Testing Tools To Level Down Your Tests'
				url 'https://github.com/Whaka-project/whakamatautau-util'
  
				scm {
					connection 'scm:git:https://vantuz-subhuman@github.com/Whaka-project/whakamatautau-util.git'
					developerConnection 'scm:git:https://vantuz-subhuman@github.com/Whaka-project/whakamatautau-util.git'
					url 'https://github.com/Whaka-project/whakamatautau-util'
				}
  
				licenses {
					license {
						name 'MIT License'
						url 'http://www.opensource.org/licenses/mit-license.php'
						distribution 'repo'
					}
				}
				
				organization {
					name 'Whaka-project'
					url 'https://github.com/Whaka-project'
				}
  
				developers {
					developer {
						id 'vantuz-subhuman'
						name 'Vantuz Subhuman'
						email 'vantuz-subhuman@yandex.ru'
						url 'https://github.com/vantuz-subhuman'
						organization 'Whaka-project'
					}
					developer {
						id 'visitorus'
						name 'visitorus'
						url 'https://github.com/visitorus'
						organization 'Whaka-project'
					}
				}
			}
		}
	}
}